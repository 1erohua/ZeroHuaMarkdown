数码管的位选是分开控制的

但是数码管的段选 **是同一控制的**，这就意味着一件事，同一个瞬间，所以数码管只能显示相同的数，那么怎么样才能实现一种计数效果呢？

> 既然同时给第 10 和第 5 脚高电平不可行，那么是不是可以先给第 5 脚高电平，第 10 脚低电平，此时，
> 让其显示数字“ 8”时，左边的数码管不显示，右边的数码管显示数字“ 8”；然后给第 10 脚高电平，第 5
> 脚低电平，此时，让其显示数字“ 1”时，左边的数码管显示数字“ 1”，右边的数码管不显示，这样就可
> 以显示数字“ 18”了。但有一个问题，多长时间切换显示的数码管呢，时间如果太长就只能看到数字―8”
> 或数字“ 1” 了，时间太短呢， 结果是显示不清晰而且显示亮度不够。**由于人眼的视觉暂留（ 人眼在观察景**
> **物时，光信号传人大脑神经，需经过一段短暂的时间，光的作用结束后，视觉形象并不立即消失，这种残**
> **留的视觉称“后像”，视觉的这一现象则被称为“视觉暂留” ）及发光二极管的余辉效应（当停止向发光**
> **二极管供电时，发光二极管亮度仍能维持一段时间），每位数码管的点亮时间为 1～2ms 时**，显示效果能满
> 足使用需要。数码管的这种驱动方式称为数码管的动态驱动，实际上就是分时轮流控制不同数码管的显示。  

也就是说，我可以这样做，当我显示当前位时，我要将其他位置为无效电平，但是这个切换过程要比较快

比如我要显示123456对不对，那我要分为6个时刻，第一个时刻先显示6，然后只亮出第一位；然后要快速切换到第二个时刻，显示5，这时候第一位 **要在视觉上保持一个不变的状态**（实际上在这个瞬间，第一位已经被赋予无无效电平）

让我们再捋一捋思路：

1. 首先我们需要实现一个计数功能，考虑到1ms这个计数可能会用的比较多，所以需要引出1ms；并且我们采用每100ms（即0.1s）计数一次，因此也需要引出一个计数总数（最大是999999，二进制下为20位）
2. 其次我们需要根据1ms来每次切换 状态，这个状态其实就是 **位状态**，就是轮到第i位，那么第i位到底要不要亮，亮哪个数。你可能看出来了，没错，我是这么思考的，我会将 **每位数码管从低位到高位（即从0到5）轮流质询一遍，无论是否亮是否灭，**从而质询到具体的那一位的时候再去分析和考虑到底该不该亮，亮哪个数
3. 接着就是要实现 **位选**，要实现位选，就需要根据 **当前的计数结果来分析哪一位亮哪一位灭**，比如001231时，应该是前两位该亮后四位该灭——切记不可以直接用每一位与0对比，否则就会导致001001这种情况下，两个1之间的0是不亮的状态。
4. 然后就是要实现 **段选**，段选的实现 **需要知道每一位的十进制数值**，但是计数结果是 **二进制的**，如果要想从中抽离出每一位数值并放入到数组，**就需要一个除法器**，有了除法器之后，再实现一个二维数值，共有6行（即6位数码管），每行是4位二进制（即表示0到9）。每次根据 **位状态** 选定当前要显示的行，然后再取出该行的数值，使用case来依次决定数码管显示哪个数。比如计数结果是001213，它的二进制为10010111101，根据这个二进制使用除法器，将 0 0 1 2 1 3这六个数字提取出来并放入二维数值。如果此时 **位状态质询到第2位（以[5:0]来看）**，这时候从二维数组提取 **二维数组的第二行** 里面的 4'b0010 （即2），case对4'b0010进行比较，然后控制 段选显示 **0**

这里有个地方比较麻烦，就是 **除法器**，教程中直接使用了/ 与%，这是十分不推荐的



## 1. 首先照例，是我们的异步模块

```verilog
//异步模块
module rst_lv2(
	input	sys_clk,
	input	sys_rst_n,
	
	output	reg	rst_lv2_out
);
	reg		lv1_rst_out;
	always @(posedge sys_clk or negedge sys_rst_n) begin
		if(!sys_rst_n)	begin
			lv1_rst_out<=1'b0;
			rst_lv2_out<=1'b0;
		end
		else begin
			lv1_rst_out<=1'b1;
			rst_lv2_out<=lv1_rst_out;
		end
	end
endmodule	
```

## 2. 然后是计数器模块

```verilog
//计数器模块
//计数器输出两个端口，可以悬空
//数码管每100ms计数一次，因此其中一个输出端口是输出100ms的计数结果
//每个数码管点亮时间是1ms，那么又要计数1ms，即另一个输出端口每1ms输出1次 1'b1
//100ms需要计数5_000_000
//1ms需要50_000


module counter(
	input 	sys_clk,
	input	rst_n,
	
	output 	reg		counter_1ms_out,	//这个仅仅输出1
	output	reg[19:0]	counter_n_out	//这个直接输出n位的计算结果
);

	reg		[15:0]	counter_50000;	//用于计数50000次——为计数1ms服务
	reg		[6:0]	counter_100ms;	//使用1ms计数100ms
	
	//先每次计算100ms，再由100ms计算显示数字
	//可以通过1ms计算100ms
	
	//1ms计数
	always @(posedge sys_clk or negedge rst_n) begin
		if (!rst_n) begin
			counter_1ms_out<=0;
			counter_50000<=0;
		end
		
		else if(counter_50000<=16'd49999)	begin
			counter_50000<=counter_50000+1;
			if(counter_1ms_out==1)
				counter_1ms_out<=0;
		end
		
		else if(counter_50000>16'd49999)	begin
			counter_50000<=0;
			counter_1ms_out<=1;
		end
	end
	
	//100ms计数
	always @(posedge sys_clk or negedge rst_n) begin
		if (!rst_n) begin
			counter_100ms<=0;
		end
		
		else if(counter_100ms<7'd99) begin
			if(counter_1ms_out)
				counter_100ms<=counter_100ms+1;
		end
		
		else if(counter_100ms>=7'd99) begin		//肯定会到99，然后99再加1回归到0，就是计数100了
			counter_100ms<=0;
		end 
	end
	
	//n位计算结果
	always @(posedge sys_clk or negedge rst_n) begin
		if(!rst_n) begin
			counter_n_out<=0;
		end
		
		else if(counter_n_out<20'd999999) begin
			if(counter_100ms==7'd99)	begin
				counter_n_out<=counter_n_out+1;
			end 
		end
		
		else if(counter_n_out>=20'd999999) begin
			counter_n_out<=0;
		end 
	end 
endmodule
```

## 3. 接着是除法器模块

```verilog
//整数除法

module	Chu_Fa_Z(
	input	[19:0]	Bei_Chu_Shu,
	
	output	reg	[3:0]	Yu_Shu,
	output	reg	[16:0]	Shang
		
);
	integer i;
	reg	[39:0]	_Bei_Chu_Shu;
	always @(Bei_Chu_Shu) begin
		Yu_Shu=0;
		Shang=0;
		_Bei_Chu_Shu={20'b0,Bei_Chu_Shu};
		
		for(i=0;i<20;i= i+ 1) begin
			_Bei_Chu_Shu=_Bei_Chu_Shu<<1;
			if(_Bei_Chu_Shu[39:20]>=20'b1010) begin
				_Bei_Chu_Shu=_Bei_Chu_Shu-{20'b1010,20'b0}+1;
			end
		end 
		
		//前n位为余数//容易知道前20位的后4位肯定是余数，
		Yu_Shu = _Bei_Chu_Shu[23:20];
		
		Shang	=	_Bei_Chu_Shu[16:0];
	end			
endmodule
//关于除法的实现,参照了这个:

```

关于除法器的实现是参照了这个扩充移位的方法，真的很很了不起

https://zhuanlan.zhihu.com/p/669362097

## 4.最后是整个位选加段选

```verilog
//数码管按照对应的显示数来确定电平
`define ZERO	8'b11000000
`define	ONE		8'b11111001
`define TWO		8'b10100100
`define	THREE	8'b10110000
`define FOUR	8'b10011001
`define FIVE	8'b10010010
`define SIX		8'b10000010
`define SEVEN	8'b11111000
`define EIGHT	8'b10000000
`define NINE	8'b10010000

`include "rst_lv2.v"
`include "counter.v"
`include "Chu_Fa_Z.v"

module sel_led_control(
	input 		sys_clk,
	input		sys_rst_n,
	

	output	reg	[5:0]	seg_sel,					//位选输出

	output	reg	[7:0]	seg_led						//段选输出
);
	wire	[19:0]	counter_n;						//数码管的计数
	
	wire			counter_1ms;					//1ms计数，每经过1ms，该线网就会输出信号1
	
	wire			rst_n;							//复位信号——经过稳定调整后的异步复位同步释放
	
	wire	[3:0] 	Demical[5:0];					//用二维数组存储十进制的计数结果（对应数码管的6位）
	
	wire 	[19:0] 	station[5:0];					//中转站——用于实现生成块的中间介质
	
	wire 	[5:0] 	translator;						//用于显示位选中哪几位是有效的
		
	reg		[2:0]	index;							//每1ms叠加一次,最多叠加6次,用于每一位的切换
	
	
	rst_lv2 u_sel_rst_lv2(.sys_clk(sys_clk),.sys_rst_n(sys_rst_n),.rst_lv2_out(rst_n));		//异步复位同步释放实例，rst_n
	
	counter U_counter(
		.sys_clk(sys_clk),
		.rst_n(rst_n),
		.counter_1ms_out(counter_1ms),
		.counter_n_out(counter_n)
	);								//放入rst_n——异步复位同步释放的复位信号只需要在主模块实现并定义即可							

	//除法器生成块，思考一下，如何得到一个十进制数的每一位？就是不断的求余，然后除数用作被除数，直到除到尽头
	genvar i;
	Chu_Fa_Z U1_ChuFaZ(.Yu_Shu(Demical[0]),.Shang(station[0]),.Bei_Chu_Shu(counter_n));
	generate 
		for(i=1;i<6;i=i+1) begin :Chu_Fa
		Chu_Fa_Z U_ChuFaZ(.Yu_Shu(Demical[i]),.Shang(station[i]),.Bei_Chu_Shu(station[i-1]));
	end
	endgenerate
	
	//重要参数index：
	//index每1ms叠加一次，最多叠加到5，用于实现位选的变换——index是几，就代表现在是第index位数码管（数码管位选从0到5）
	always @(posedge sys_clk or negedge rst_n) begin
		if (!rst_n) begin
			index<=0;
		end
		
		else if(index<5) begin
			if(counter_1ms)	begin
				index<=index+1;	
			end 
		end
		
		else if(index>=5) begin
			index<=0;
		end 
	end
	
	//由于水平有限，不知道怎么才能把类似于001231和001001这种转换成001111，最后是选择了比较法
	//用于确定位选哪几位的灯该亮该灭
	assign translator=(counter_n==0 ? 6'b0 :
                  (counter_n>=1 && counter_n <=9 ? 6'b1 :
                  (counter_n>=10 && counter_n<=99 ? 6'b11 :
                  (counter_n>=100 && counter_n<=999 ? 6'b111 :
                  (counter_n>=1000 && counter_n<=9999 ? 6'b1111 :
                  (counter_n>=10000 && counter_n<=99999 ? 6'b11111 :
                  (counter_n>=100000 && counter_n<=999999 ? 6'b111111 : 6'bxxxxxx)))))));
	
	//位选的控制
	//控制思路：比如我现在的计数结果是 20'd1006
	//那么translator的结果应该是 6'b001111
	//然后这时候就要使用index，使的位选每次显示 由translator指定的那index位的值（应该是该值的取反，因为低电平有效）
	always @(posedge sys_clk or negedge rst_n) begin
		if (!rst_n) begin
			seg_sel<=6'b111111;
		end
		else begin
			seg_sel[index]<=~translator[index];
			if(index!=0) begin	//这里还要把前一位置熄灭
				seg_sel[index-1]<=1;
			end 
		end
	end
	
	//段选控制，这个反而还是比较简单，只要解决了除法器的那部分
	//其实index才是核心的关键，可以看做它是对1ms的六态流水
	//段选只关心每个1ms时，它应该显示哪个数据，所以它需要的是 001234 这样每一位都存着具体的数值的
	//位选只关心每个1ms时,它应该显示哪几位,所以它需要的是	001111	这样每一位都表示着有效位的
	always @(posedge sys_clk or negedge rst_n) begin
		if(!rst_n) begin
			seg_led<=0;
		end 
		
		else begin
			case(Demical[index])
			4'b0000:seg_led<=`ZERO;
			4'b0001:seg_led<=`ONE;
			4'b0010:seg_led<=`TWO;
			4'b0011:seg_led<=`THREE;
			4'b0100:seg_led<=`FOUR;
			4'b0101:seg_led<=`FIVE;
			4'b0110:seg_led<=`SIX;
			4'b0111:seg_led<=`SEVEN;
			4'b1000:seg_led<=`EIGHT;
			4'b1001:seg_led<=`NINE;
			endcase
		end 
	end 
	
endmodule

module	seg_led_dynamic(
	input	sys_clk,
	input	sys_rst_n,
	output	[5:0]	seg_sel,
	output	[7:0]	seg_led
);
	sel_led_control U_sel_led_control(
	.sys_clk(sys_clk),
	.sys_rst_n(sys_rst_n),
	.seg_led(seg_led),
	.seg_sel(seg_sel)
	);

endmodule	
```

